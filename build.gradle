plugins {
    id 'org.springframework.boot' version '3.3.4'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'java'
    id 'project-report'
    id "com.github.ben-manes.versions" version "0.51.0"
    id "com.diffplug.spotless" version "6.25.0"
}

group = 'com.github.lhotari'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

spotless {
    java {
        removeUnusedImports()
        prettier(['prettier': '3.3.3', 'prettier-plugin-java': '2.6.4'])
                .config(['plugins'      : ['prettier-plugin-java'],
                         'trailingComma': 'none',
                         'useTabs'      : false,
                         'tabWidth'     : 4,
                         'semi'         : true,
                         'singleQuote'  : false,
                         'printWidth'   : 120,
                         'arrowParens'  : 'avoid'])
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

sourceSets {
    intTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

def pulsarVersion = '4.0.1'

// when using Spring Boot with io.spring.dependency-management plugin, this is the way to change the Pulsar Java client version
// See https://docs.spring.io/spring-boot/appendix/dependency-versions/properties.html
ext['pulsar.version'] = pulsarVersion

dependencies {
    // this would be used if io.spring.dependency-management plugin wasn't used
    //implementation enforcedPlatform("org.apache.pulsar:pulsar-bom:${pulsarVersion}")
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-log4j2'
    implementation 'org.springframework.boot:spring-boot-starter-pulsar-reactive'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    intTestAnnotationProcessor 'org.projectlombok:lombok'
    intTestCompileOnly 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.springframework.cloud:spring-cloud-contract-wiremock:4.1.0'
    testImplementation 'org.awaitility:awaitility'
    intTestImplementation 'org.testcontainers:pulsar'
}

configurations {
    all {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
}

test {
    useJUnitPlatform()
}

configurations {
    intTestImplementation.extendsFrom testImplementation
    intTestRuntimeOnly.extendsFrom testRuntimeOnly
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath
    shouldRunAfter test
    useJUnitPlatform()
    environment('PULSAR_CONTAINER_IMAGE', "apachepulsar/pulsar:${pulsarVersion}")
}

check.dependsOn integrationTest

